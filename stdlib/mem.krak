__if_comp__ __C__ simple_passthrough """
	#include <stdlib.h>
"""

/* we have a template versions so we don't have to cast (because we don't have that yet) */

template <T> |T*| malloc(|int| size) {
    |T*| memPtr;
	__if_comp__ __C__ {
        simple_passthrough( size = size, memPtr = memPtr : memPtr = memPtr :) """
			memPtr = malloc(size);
		"""
	}
	return memPtr;
}

template <T> |void| free(|T*| memPtr) {
	__if_comp__ __C__ {
		simple_passthrough(memPtr = memPtr ::) """
			free(memPtr);
		"""
	}
}

template <T> |int| sizeof() {
	|T| testObj;
    |int| result;
	__if_comp__ __C__ {
		simple_passthrough(testObj = testObj : result = result:) """
			int result = sizeof(testObj);
		"""
	}
	return result;
}

template <T> |T*| new(|int| count) {
	return malloc<T>( sizeof<T>() * count );
}

template <T> |T*| new() {
	return new<T>(1);
}

/* We specilize on the trait Destructable to decide on whether or not the destructor should be called */
template <T> |void| delete(|T*| toDelete, |int| itemCount) {
    delete<T>(toDelete);
}

/* Calling this with itemCount = 0 allows you to delete destructable objects without calling their destructors. */
template <T(Destructable)> |void| delete(|T*| toDelete, |int| itemCount) {
    for (|int| i = 0; i < itemCount; i++;)
        toDelete[i].destruct();
    delete<T>(toDelete);
}

/* We specilize on the trait Destructable to decide on whether or not the destructor should be called */
template <T> |void| delete(|T*| toDelete) {
    free<T>(toDelete);
}

template <T(Destructable)> |void| delete(|T*| toDelete) {
    toDelete->destruct();
    free<T>(toDelete);
}
