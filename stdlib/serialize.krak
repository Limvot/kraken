import vector
import mem
import util

fun serialize<T(Serializable)>(it: T): vector::vector<char> {
    return it.serialize()
}

fun serialize<T>(it: T): vector::vector<char> {
    var char_data = (&it) cast *char
    var toRet = vector::vector<char>()
    for (var i = 0; i < #sizeof<T>; i++;)
        toRet.add(char_data[i])
    return toRet
}

// dead simple wrapper for ease of use
fun unserialize<T>(it: ref vector::vector<char>): T {
    return unserialize<T>(it, 0).first
}
fun unserialize<T>(it: ref vector::vector<char>, pos: int): util::pair<T,int> {
    return util::make_pair(*(it.getBackingMemory()+pos) cast *T, pos + (#sizeof<T>) cast int)
}
fun unserialize<T(Serializable)>(it: ref vector::vector<char>, pos: int): util::pair<T,int> {
    var toRet: T
    pos = toRet.unserialize(it, pos)
    return util::make_pair(toRet, pos)
}


