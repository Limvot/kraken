import mem
import io
import os
import string
import set
import map
import vector
import serialize


// maybe my favorite function
fun do_nothing() {}

fun is_object<T>(): bool return false
fun is_object<T(Object)>(): bool return true

fun error(message: *char) error(string::string(message));
fun error(message: string::string) {
    io::printlnerr("****ERROR****")
    io::printlnerr(message)
    os::exit(-1)
}
fun assert(works: bool, message: *char) {
    if (!works)
        error(message)
}
fun assert(works: bool, message: string::string) {
    if (!works)
        error(message)
}

fun deref_equality<T>(a: *T, b: *T): bool {
    if ( (a && b && !(*a == *b)) || (a && !b) || (!a && b) )
        return false
    return true
}

fun max<T>(a: T, b: T): T {
    if (a > b)
        return a;
    return b;
}

fun min<T>(a: T, b: T): T {
    if (a > b)
        return b;
    return a;
}

fun hash<T(Hashable)>(item: T): ulong return item.hash()
fun hash<T>(item: *T): ulong return (item) cast ulong
fun hash(item: char): ulong return (item) cast ulong
fun hash(item: uchar): ulong return (item) cast ulong
fun hash(item: short): ulong return (item) cast ulong
fun hash(item: ushort): ulong return (item) cast ulong
fun hash(item: int): ulong return (item) cast ulong
fun hash(item: uint): ulong return (item) cast ulong
fun hash(item: long): ulong return (item) cast ulong
fun hash(item: ulong): ulong return (item) cast ulong

// default hash
fun hash<T>(item: T): ulong {
    io::println("using empty hash - please do not do!")
    return (0) cast ulong
}

fun make_pair<T,U>(first: T, second: U): pair<T,U> {
    var it.construct(first, second): pair<T,U>
    return it
}

// little ugly, but it works
fun unpack<T,U>(first: ref T, second: ref U): unpack_dummy<T,U> {
    var toRet: unpack_dummy<T,U>
    toRet.first = &first
    toRet.second = &second
    return toRet
}
obj unpack_dummy<T,U> {
    var first: *T
    var second: *U
    fun operator=(p: ref pair<T,U>) {
        *first = p.first
        *second = p.second
    }
}

obj pair<T,U> (Object, Serializable, Hashable) {
    var first: T
    var second: U

    fun construct(firstIn: ref T, secondIn: ref U): *pair<T,U> {
        mem::maybe_copy_construct(&first, &firstIn)
        mem::maybe_copy_construct(&second, &secondIn)
        return this
    }

    fun construct(): *pair<T,U> {
        mem::maybe_construct(&first)
        mem::maybe_construct(&second)
        return this
    }

    fun copy_construct(old: *pair<T,U>):void {
        mem::maybe_copy_construct(&first, &old->first)
        mem::maybe_copy_construct(&second, &old->second)
    }

    fun destruct():void {
        mem::maybe_destruct(&first)
        mem::maybe_destruct(&second)
    }
    fun serialize(): vector::vector<char> {
        return serialize::serialize(first) + serialize::serialize(second)
    }
    fun unserialize(it: ref vector::vector<char>, pos: int): int {
        // can't use unpack :( (b/c we can't make an already constructed empty one)
        var first_pair = serialize::unserialize<T>(it, pos)
        var second_pair = serialize::unserialize<U>(it, first_pair.second)
        mem::maybe_copy_construct(&first, &first_pair.first)
        mem::maybe_copy_construct(&second, &second_pair.first)
        return second_pair.second
    }

    fun hash():ulong return hash(first) ^ hash(second)

    // the old unnecessary template to prevent generation
    // if not used trick (in this case, changing out U with V)
    fun operator==<V>(other: ref pair<T,V>): bool {
        return first == other.first && second == other.second
    }
}

fun range(end:int): range {
    var toRet.construct(0, end, 1): range
    return toRet
}

fun range(begin: int, end:int): range {
    var toRet.construct(begin, end, 1): range
    return toRet
}
fun range(begin: int, end:int, step: int): range {
    var toRet.construct(begin, end, step): range
    return toRet
}

obj range {
    var begin: int
    var end: int
    var step: int
    fun construct(beginIn: int, endIn: int, stepIn: int) : *range {
        begin = beginIn
        end = endIn
        step = stepIn
    }
    fun for_each(func: fun(int):void):void {
        for (var i = begin; i < end; i+= step;)
            func(i)
    }
    fun map<T>(func: fun(int): T): vector::vector<T> {
        var ret.construct( (end-begin)/step + 1 ) : vector::vector<T>
        for (var i = begin; i < end; i+= step;)
            ret.addEnd(func(i))
        return ret
    }
    fun any_true(func: fun(int):bool):bool {
        for (var i = begin; i < end; i+= step;)
            if (func(i))
                return true
        return false
    }
}

// a function that allows the safe deletion of recursive and complicated data structures
fun safe_recursive_delete<T>(first: *T, addingFunc: fun(*T): set::set<*T>) {
    var toDelete = set::set<*T>()
    var next = set::set(first)
    while (toDelete != next) {
        toDelete = next
        toDelete.for_each( fun(it: *T) next.add(addingFunc(it)); )
    }
    toDelete.for_each( fun(it: *T) mem::delete(it); )
}

// a function that allows the safe cloning of recursive and complicated data structures
// cloneing func is the func that does the cloning, it takes in a recursive clone function and
// a register clone function
fun safe_recursive_clone<T>(first: *T, cloningFunc: fun(*T, fun(*T):*T, fun(*T):void): void): *T {
    var rec_map = map::map<*T,*T>()
    // can't do type infrence if you need the type inside the expression...
    var rec_it: fun(*T):*T = fun(it: *T): *T {
        if (!rec_map.contains_key(it))
            cloningFunc(it, rec_it, fun(cloned: *T) { rec_map[it] = cloned; })
        return rec_map[it]
    }
    return rec_it(first)
}

