<!doctype html>
<html lang="en-us">
<meta charset="UTF-8">
    <head>
	 	<style>
            h1, h2 ,h3 { line-height:1.2; }
			body {
                max-width: 45em;
				margin: 1em auto;
				padding: 0 .62em;
				font: 1.2em/1.62 sans-serif;
			}

            th { text-align: center; }
            th, td { padding: 0.5em; }
            table, td {
                border: 1px solid #333;
                text-align: right;
            }
            thead, tfoot {
                background-color: #000;
                color: #fff;
            }

            #hello_editor { height: 7em; width: 70em; }
            #hello_output { height: 7em; width: 70em; }
            #prelude_editor { height: 54em; width: 70em; }
            #prelude_output { height: 7em; width: 70em; }
            #method_editor { height: 58em; width: 70em; }
            #method_output { height: 7em; width: 70em; }
            #bf_editor { height: 67em; width: 70em; }
            #bf_output { height: 7em; width: 70em; }
            #fib_editor { height: 8em; width: 70em; }
            #fib_output { height: 7em; width: 70em; }
		</style>
    </head>
    <body>
	    <header><h2>Nathan Braswell's Current Programming Language / Compiler Research</h2></header>
        Repository: <a title="Kraken on GitHub" href="https://github.com/limvot/kraken">https://github.com/limvot/kraken</a>
        <br> <br>
        <b>Table of Contents:</b> <i>If you're impatient, jump to the code examples!</i>
        <ul>
            <li><a href="#concept">Concept</a>
            <li><a href="#about">About</a>
            <li><a href="#hello_example">Example: Hello World</a>
            <li><a href="#vau_core">Vau as a core</a>
            <li><a href="#method_example">Example: Implementing Methods</a>
            <li><a href="#bf_example">Example: Embedding BF</a>
            <li><a href="#next_steps">Next Steps</a>
        </ul>
        <a name="concept"/>
	    <h3>Concept:</h3>
	    <ul>
		    <li> Minimal, close to the metal Kernel/Scheme (operate on words, bytes, arrays) as AST / core language, with Kernel/Vau calculus inspiration oblivating the need for non-reader macros (<a title="Kernel/Vau calculus thesis" href="https://web.wpi.edu/Pubs/ETD/Available/etd-090110-124904/unrestricted/jshutt.pdf">Kernel/Vau calculus thesis</a>)
            <li> Full Context-free (and eventually, context sensitive) reader macros using FUN-GLL (<a title="fun-gll paper" href="https://www.sciencedirect.com/science/article/pii/S2590118420300058">FUN-GLL paper</a>) to extend language's syntax dynamically
	 	    <li> Implement Type Systems as Macros (but using Vaus instead of macros) (<a title="type systems as macros paper 1" href="http://www.ccs.neu.edu/home/stchang/pubs/ckg-popl2017.pdf">paper, up to System FÏ‰</a>) (<a title="type systems as macros paper 2" href="https://www.ccs.neu.edu/home/stchang/pubs/cbtb-popl2020.pdf">second paper, up to dependent types</a>)
		    <li> Use above "type systems as vaus" to create richer language and embed entire other programming languages (syntax, semantics, and type system) for flawless interop/FFI (C, Go, Lua, JS, etc)
            <li> File is interpreted, and then if "main" exists it is compiled, spidering backwards to referenced functions and data (Allows interpreted code to do metaprogramming, dependency resolution, generate code, etc, which is then compiled)
            <li> Regionalized Value State Dependence Graph as backend-IR, enabling simpler implementations of powerful optimizations (<a title="RSVDG paper" href="https://arxiv.org/pdf/1912.05036.pdf">RSVDG paper</a>) so that embedded languages have good performance when compiled with little code
	    </ul>
        <a name="about"/>
	    <h3> About:</h3>
	    <p> Currently, I am bootstrapping this new core Lisp out of my prior compiler for my programming language, Kraken. I have implemented the first version of the FUN-GLL algorithm and have working vaus and context-free reader macros.
        <p> The general flow is that the input files will be executed with the core Lisp interpreter, and if there is a "main" symbol defined the compiler emits C code for that function & all other functions & data that it references. In this way the language supports very powerful meta-programming at compile time, including adding syntax to the language, arbitrary computation, and importing other files, and then compiles into a static executable.
	    <p> Below are a few examples of using the vau / live grammar modification / context-free reader macros to implement basic methods as well as embed the BF language into the core Lisp. The core Lisp implementation has been compiled to WebAssembly and should be able to run in your browser. Feel free to make edits and play around below.
	    <br>
Note that the current implementation is inefficient, and sometimes has problems running in phone web browsers.
        <a name="hello_example"/>
	    <h4>Runnable Example Code:</h4>
        <button onclick="executeKraken(hello_editor.getValue(), 'hello_output')"><b>Run</b></button> <br>
<div id="hello_editor">; Of course
(println "Hello World")
; Just print 3
(println "Math works:" (+ 1 2))
</div>
        <h4>Output:</h4>
        <textarea id="hello_output">Output will appear here</textarea>
        <a name="vau_core"/>
        <h4>Vau/Kernel as simple core:</h4>
        By constructing our core language on a very simple Vau/Kernel base, we can keep the base truely tiny, and build up normal Lisp functions and programming language features in the language itself. This should help implement other programming languages concisely, and will hopefully make optimization easier and more broadly applicable.
        <br>
        Below is the current prelude that adds quoting, quasiquoting, syntax for arrays and quoting/quasiquoting, do, if, let, and even lambda itself!
        <br>
        <button onclick="executeKraken(prelude_editor.getValue(), 'prelude_output')"><b>Run</b></button> <br>
<div id="prelude_editor">

(set! quote (vau _ (x) x))
(set! lambda (vau se (p b) (wrap (eval (array vau (quote _) p b) se))))
(set! current-env (vau de () de))
(set! fun (vau se (n p b) (eval (array set! n (array lambda p b)) se)))

; do_helper is basically mapping eval over statements, but the last one is in TCO position
; a bit of a hack, using cond to sequence (note the repitition of the eval in TCO position if it's last,
; otherwise the same eval in cond position, and wheather or not it returns a truthy value, it recurses in TCO position)
(fun do_helper (s i se) (cond (= i (len s)) nil
                              (= i (- (len s) 1)) (eval (idx s i) se)
                              (eval (idx s i) se) (do_helper s (+ i 1) se)
                              true                (do_helper s (+ i 1) se)))
(set! do (vau se (& s) (do_helper s 0 se)))

(fun concat_helper (a1 a2 a3 i) (cond (< i (len a1))              (do (set-idx! a3 i (idx a1 i))              (concat_helper a1 a2 a3 (+ i 1)))
                                      (< i (+ (len a1) (len a2))) (do (set-idx! a3 i (idx a2 (- i (len a1)))) (concat_helper a1 a2 a3 (+ i 1)))
                                      true a3))
(fun concat (a1 a2) (concat_helper a1 a2 (array-with-len (+ (len a1) (len a2))) 0))

(add_grammar_rule (quote form) (quote ( "'" optional_WS form )) (vau de (_ _ f) (array quote (eval f de))))
(add_grammar_rule 'form '( "\\[" optional_WS space_forms optional_WS "\\]" ) (vau de (_ _ fs _ _) (concat (array array) (eval fs de))))

(fun vapply (f p ede) (eval (concat [f]          p) ede))
(fun lapply (f p)     (eval (concat [(unwrap f)] p) (current-env)))

(set! let1 (vau de (s v b) (eval [[vau '_ [s] b] (eval v de)] de)))
(set! let (vau de (vs b) (cond (= (len vs) 0) (eval b de) true (vapply let1 [(idx vs 0) (idx vs 1) [let (slice vs 2 -1) b]] de))))

(set! if (vau de (con than & else) (cond 
                                        (eval con de) (eval than de)
                                        (> (len else) 0) (eval (idx else 0) de)
                                        true nil)))
(fun map (f l)
    (let (helper (lambda (f l n i recurse)
                                    (if (= i (len l))
                                        n
                                        (do (set-idx! n i (f (idx l i)))
                                            (recurse f l n (+ i 1) recurse)))))
         (helper f l (array-with-len (len l)) 0 helper)))
(fun flat_map (f l)
    (let (helper (lambda (f l n i recurse)
                                    (if (= i (len l))
                                        n
                                        (recurse f l (concat n (f (idx l i))) (+ i 1) recurse))))
         (helper f l (array) 0 helper)))
(fun map_with_idx (f l)
    (let (helper (lambda (f l n i recurse)
                                    (if (= i (len l))
                                        n
                                        (do (set-idx! n i (f i (idx l i)))
                                            (recurse f l n (+ i 1) recurse)))))
         (helper f l (array-with-len (len l)) 0 helper)))

(fun print_through (x) (do (println x) x))
(fun is_pair? (x) (and (array? x) (> (len x) 0)))

(set! quasiquote (vau de (x)
    (cond (is_pair? x)
                (cond (and (symbol? (idx x 0)) (= (get-text (idx x 0)) "unquote"))
                            (eval (idx x 1) de)
                      true
                            (cond (and (is_pair? (idx x 0)) (symbol? (idx (idx x 0) 0)) (= (get-text (idx (idx x 0) 0)) "splice-unquote"))
                                        (concat (eval (idx (idx x 0) 1) de) (vapply quasiquote [(slice x 1 -1)] de))
                                  true
                                        (concat [(vapply quasiquote [(idx x 0)] de)] (vapply quasiquote [(slice x 1 -1)] de))))
          true x)))

(add_grammar_rule 'form '("`" optional_WS form) (lambda (_ _ f) ['quasiquote f]))
(add_grammar_rule 'form '("~" optional_WS form) (lambda (_ _ f) ['unquote f]))
(add_grammar_rule 'form '("," optional_WS form) (lambda (_ _ f) ['splice-unquote f]))


(println "now with both array and quasiquote syntax, check out " `(1 2 3 ~(+ 7 8) ,[ 5 6 7]))
</div>
        <h4>Output:</h4>
        <textarea id="prelude_output">Output will appear here</textarea>
        <a name="method_example"/>
        <h4>Method Example:</h4>
        Let's use our meta system (attaching objects to other objects) to implement basic objects/methods, a new lambda syntax, a new block syntax, and string interpolation!
        We will attach a array of alternating symbols / functions (to make this example simple, since maps aren't built in) to our data as the meta, then look up methods on it when we perform a call. The add_grammar_rule function modifies the grammar/parser currently being used to parse the file and operates as a super-powerful reader macro. We use it in this code to add a rule that transforms <pre><code>a.b(c, d)</code></pre> into <pre><code>(method-call a 'b c d)</code></pre> where method-call is the function that looks up the symbol 'b on the meta object attached to a and calls it with the rest of the parameters.
        Note also the block ({}) syntax that translates to nested do/let expressions, the nicer lambda syntax, and the string interpolation (that even works nested!).
        <br>
        <button onclick="executeKraken(method_editor.getValue(), 'method_output')"><b>Run</b></button>
        <br>
        <div id="method_editor">
; Load prelude so we get fun, lambda, if, quoting, etc
(load-file "./k_prime_stdlib/prelude.kp")
; First quick lookup function, since maps are not built in
(fun get-value-helper (dict key i) (if (>= i (len dict))
                                                nil
                                                (if (= key (idx (idx dict i) 0))
                                                    (idx (idx dict i) 1)
                                                    (get-value-helper dict key (+ i 1)))))
(fun get-value (dict key) (get-value-helper dict key 0))

; Our actual method call function
(fun method-call (object method & arguments) (let (method_fn (get-value (meta object) method))
                                                            (if (= method_fn nil)
                                                                (println "no method " method)
                                                                (lapply method_fn (concat [object] arguments)))))
; Some nice syntactic sugar for method calls
; No params
(add_grammar_rule 'form ['form "\\." 'atom]
                            (lambda (o _ m) `(method-call ~o '~m)))
; params
(add_grammar_rule 'form ['form "\\." 'atom 'optional_WS "\\(" 'optional_WS 'space_forms 'optional_WS "\\)"]
                            (lambda (o _ m _ _ _ p _ _) `(method-call ~o '~m ,p)))

; object creation
(fun make_constructor (members methods)
    (eval `(lambda  ~members
        (with-meta [,members]
            [,(map_with_idx (lambda (i x) [array `'~x (lambda (o) (idx o i))]) members)
             ,(map (lambda (x) [array `'~(idx x 0) (idx x 1)]) methods)]))))

; object syntax
(add_grammar_rule 'form ["obj" 'WS 'atom "\\(" ['optional_WS 'atom] * 'optional_WS "\\)" 'optional_WS "{" 'optional_WS ['atom 'optional_WS 'form 'optional_WS] *  "}"] (lambda (_ _ name _ members _ _ _ _ _ methods _)
        `(set! ~name (make_constructor [,(map (lambda (x) `'~(idx x 1)) members)]
                                       [,(map (lambda (x) `['~(idx x 0) ~(idx x 2)]) methods)]))))

; Lambda syntax
(add_grammar_rule 'form ["\\|" 'optional_WS [ 'atom 'optional_WS ] * "\\|" 'optional_WS 'form ]
                    (lambda (_ _ params _ _ body) `(lambda (,(map (lambda (x) (idx x 0)) params)) ~body)))

; {} body translated to do and let
(add_grammar_rule 'block_member [ 'form ] |x| [x])
(add_grammar_rule 'block_member [ "let" 'optional_WS 'atom 'optional_WS "=" 'optional_WS 'form ]
                            |_ _ name _ _ _ rhs| `(~name ~rhs))
(fun construct_body (is_do current to_add i)
    (if (> (len to_add) i)
        (cond (and is_do (= (len (idx to_add i)) 1)) (construct_body true (concat current [(idx (idx to_add i) 0)]) to_add (+ i 1))
              (= (len (idx to_add i)) 1)             (concat current [(construct_body true [do (idx (idx to_add i) 0)] to_add (+ i 1))])
              true                                   (concat current [(construct_body false [let [(idx (idx to_add i) 0) (idx (idx to_add i) 1)] ] to_add (+ i 1))]))
        current))
(add_grammar_rule 'form ["{" 'optional_WS [ 'block_member 'optional_WS ] * "}"]
    |_ _ inner _| (construct_body true [do] (map |x| (idx x 0) inner) 0))

; Call functions with function first, c style (notice no whitespace)
(add_grammar_rule 'form [ 'form 'call_form ] |f ps| (concat [f] ps))

; fun syntax
(add_grammar_rule 'form [ "fun" 'WS 'atom 'optional_WS "\\(" 'optional_WS [ 'atom 'optional_WS ] * "\\)" 'optional_WS 'form ]
    |_ _ name _ _ _ params _ _ body| `(fun ~name (,(map |x| (idx x 0) params)) ~body))

; string interpolation
fun remove_dollar(done to_do i j) (cond (>= j (- (len to_do) 2))          (str done (slice to_do i -1))
                                        (= "\\$" (slice to_do j (+ j 2))) (remove_dollar (str done (slice to_do i j) "$") to_do (+ j 2) (+ j 2))
                                        true                              (remove_dollar done to_do i (+ j 1)))
fun fixup_str_parts(s) (remove_dollar "" (slice s 0 -2) 0 0)
(add_grammar_rule 'form [ "$\"" [ "(#|[%-[]|	|[]-~]|(\\\\)|(\\n)|(\\t)|(\\*)|(\\\\$)|
|[ -!]|(\\\\\"))*$" 'form ] * "(#|[%-[]|	|[]-~]|(\\\\)|(\\n)|(\\t)|(\\*)|(\\\\$)|
|[ -!]|(\\\\\"))*\"" ]
    |_ string_form_pairs end| `(str ,( flat_map |x| [ (fixup_str_parts (idx x 0)) (idx x 1) ] string_form_pairs) ~(fixup_str_parts end)))

(println $"unu |\$| $$"inner $(+ 1 2) post-inner" sual")

obj Point( x y ) {
    add |self other| { Point((+ self.x other.x) (+ self.y other.y)) }
    sub |self other| { Point((- self.x other.x) (- self.y other.y)) }
    to_str |self| { str("x: " self.x ", y: " self.y) }
}

fun say_hi(name) {
    println("hayo" name)
}

fun test() {
    let plus_1 = |x| (+ x 1)
    let a = 1
    let b = plus_1(a)
    println("some" b)

    say_hi("Marcus")

    let p1 = Point(1 2)
    let p2 = Point(3 4)
    let p3 = p1.add(p2)
    let p4 = p1.sub(p2)

    println("p1:" p1.to_str)
    println("p2:" p2.to_str)
    println("p3:" p3.to_str)
    println("p4:" p4.to_str)

    (+ a b)
}
println("Test result is" test())
</div>
        <h4>Output: </h4>
        <textarea id="method_output">Output will appear here</textarea>
        <a name="bf_example"/>
        <h4>More Complicated Example: BF as an embedded language</h4>
        <button onclick="executeKraken(bf_editor.getValue(), 'bf_output')"><b>Run</b></button> <br>
<div id="bf_editor">

(load-file "./k_prime_stdlib/prelude.kp")

; We don't have atoms built in, mutable arrays
; are our base building block. In order to make the
; following BF implementation nice, let's add atoms!
; They will be implmented as length 1 arrays with nice syntax for deref
(fun make-atom (x) [x])
(fun set-atom! (x y) (set-idx! x 0 y))
(fun get-atom (x) (idx x 0))
(add_grammar_rule 'form ["@" 'form] (lambda (_ x) `(get-atom ~x)))

; Now begin by defining our BF syntax & semantics
; Define our tokens as BF atoms
(add_grammar_rule 'bfs_atom ["<"]   (lambda (_) '(set-atom! cursor (- @cursor 1))))
(add_grammar_rule 'bfs_atom [">"]   (lambda (_) '(set-atom! cursor (+ @cursor 1))))
(add_grammar_rule 'bfs_atom ["\\+"] (lambda (_) '(set-idx! tape @cursor (+ (idx tape @cursor) 1))))
(add_grammar_rule 'bfs_atom ["-"]   (lambda (_) '(set-idx! tape @cursor (- (idx tape @cursor) 1))))
(add_grammar_rule 'bfs_atom [","]   (lambda (_) '(let (value (idx input @inptr))
                                                      (do (set-atom! inptr (+ 1 @inptr))
                                                          (set-idx! tape @cursor value)))))
(add_grammar_rule 'bfs_atom ["."]   (lambda (_) '(set-atom! output (concat [(idx tape @cursor)] @output))))

; Define strings of BF atoms
(add_grammar_rule 'bfs ['bfs_atom *] (lambda (x) x))

; Add loop as an atom
; (note that closure cannot yet close over itself by value, so we pass it in)
(add_grammar_rule 'bfs_atom ["\\[" 'bfs "]"] (lambda (_ x _)
                `(let (f (lambda (f)
                                (if (= 0 (idx tape @cursor))
                                    nil
                                    (do ,x (f f)))))
                       (f f))))

; For now, stick BFS rule inside an unambigious BFS block
; Also add setup code
(add_grammar_rule 'form ["bf" 'optional_WS "{" 'optional_WS 'bfs 'optional_WS "}"]
                        (lambda (_ _ _ _ x _ _)
                           `(lambda (input)
                             (let (
                                    tape   (array 0 0 0 0 0)
                                    cursor (make-atom 0)
                                    inptr  (make-atom 0)
                                    output (make-atom (array))
                                    )
                                (do (println "beginning bfs") ,x (idx output 0))))))

; Let's try it out! This BF program prints the input 3 times
(println (bf { ,>+++[<.>-] } [1337]))
; we can also have it compile into our main program
(fun main () (do (println "BF: " (bf { ,>+++[<.>-] } [1337])) 0))
</div>
        <h4>Output: </h4>
        <textarea id="bf_output">Output will appear here</textarea>
        <a name="benchmarks"/>
        <!--<h3>Performance Benchmarks</h3>-->
        <!--<p>Performance is quite poor (for the interpreter mainly, the C compiler seems to be smart enough to make even the very inefficient generated C code fast), as almost no work has gone into it as of yet.-->
        <!--We are currently focusing on the FUN-GLL macros and creating a more fully-featured language on top of the core Lisp using them. We will focus more on performance with the implementation of the functional persistent data structures and the self-hosting rewrite, and performance will be the main focus of the RVSDG IR part of the project.-->
        <!--<p> Even so, it is worth keeping a rough estimate of performance in mind. For this, we have compiled a very basic benchmark below, with more benchmark programs (sorting, etc) to be included as the language gets developed:-->
        <!--<br>-->
        <!--<table>-->
            <!--<thead>-->
                <!--<tr>-->
                    <!--<th></th>-->
                    <!--<th>Core Lisp Interpreter</th>-->
                    <!--<th>Core Lisp Compiled to C</th>-->
                    <!--<th>Hand-written C</th>-->
                <!--</tr>-->
            <!--</thead>-->
            <!--<tbody>-->
                <!--<tr>-->
                    <!--<td><b>Fibonacci(27)</b></td>-->
                    <!--<td>51.505s</td>-->
                    <!--<td>0.007s</td>-->
                    <!--<td>0.002s</td>-->
                <!--</tr>-->
            <!--</tbody>-->
        <!--</table>-->
        <!--<br>-->
        <!--Here is the core Lisp code run / compiled by the above test, which you can run in your web browser. The hand-written C code is an exact translation of this into idiomatic C.-->
        <!--<br><i>Note: N is lowered in the web demo so WebAssembly doesn't run out of memory.</i>-->
        <!--<a name="fib_example"/>-->
		<!--<h4>Fibonacci:</h4>-->
        <!--<button onclick="executeKraken(fib_editor.getValue(), 'fib_output')"><b>Run</b></button> <br>-->
<!--<div id="fib_editor">(def! fib (fn* (n) (cond (= 0 n) 0-->
                         <!--(= 1 n) 1-->
                         <!--true    (+ (fib (- n 1)) (fib (- n 2))))))-->
<!--(let* (n 16)-->
    <!--(println "Fib(" n "): " (fib n)))-->
<!--</div>-->
        <!--<h4>Output:</h4>-->
        <!--<textarea id="fib_output">Output will appear here</textarea>-->
        <a name="next_steps"/>
        <h3>Next Steps</h3>
        <ul>
            <li> Implement persistent functional data structures
                <ul>
                    <li> Hash Array-Mapped Trie (HAMT) / Relaxed Radix Balance Tree (RRB-Tree)
                    <li> Hash Map based on the above
                        <li> Hash Set based on the above
                </ul>
            <li> Prototype Type Systems as Macros, may require macro system rewrite/upgrade
            <li> Sketch out Kraken language on top of core Lisp, includes basic Hindley-Milner type system implemented with Macros and above data structures
            <li> Re-self-host using functional approach in above Kraken language
            <li> Use Type System Macros to implement automatic transient creation on HAMT/RBB-Tree as an optimization
            <li> Implement RVSDG IR and develop best bang-for-buck optimizations using it
        </ul>


        <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/ace/1.4.11/ace.min.js"></script>
        <script>
            ace.config.set('basePath', 'https://cdnjs.cloudflare.com/ajax/libs/ace/1.4.11/')
            var hello_editor = ace.edit("hello_editor")
            var prelude_editor = ace.edit("prelude_editor")
            var method_editor = ace.edit("method_editor")
            var bf_editor = ace.edit("bf_editor")
            //var fib_editor = ace.edit("fib_editor")
            //for (let editor of [hello_editor, method_editor, bf_editor, fib_editor]) {
            for (let editor of [hello_editor, prelude_editor, method_editor, bf_editor]) {
                editor.session.setMode("ace/mode/clojure")
                editor.setOption("displayIndentGuides", false)
                editor.setShowPrintMargin(false)
            }
            var output_name = ""
            var Module = {
                noInitialRun: true,
                onRuntimeInitialized: () => {
                },
                print: txt => {
                    document.getElementById(output_name).value += txt + "\n";
                },
                printErr: txt => {
                    document.getElementById(output_name).value += "STDERR:[" + txt + "]\n";
                }
            };
            function executeKraken(code, new_output_name) {
                output_name = new_output_name
                document.getElementById(new_output_name).value = "running...\n";
                Module.callMain(["-C", code]);
            }
        </script>
        <script type="text/javascript" src="k_prime.js"></script>
    </body>
</html>
